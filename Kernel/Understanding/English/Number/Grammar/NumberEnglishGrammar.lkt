// **************************************************
//
//	Rules for numbers in english.
//
// **************************************************
// 
//	PROJECT	:	Fluency
//	MODULE  :	Kernel
//	PACKAGE	:	Understanding.English.Number.Grammar
//	FILE	:	NumberEnglishGrammar.lkt
// 
// **************************************************

// Output of this package
(Number_Rule01_1 :
 	[ ParameterValue -> Number ]
	{
		^.Number <- #1;
	}
)

(Number_Rule01_2 :
 	[ Number -> < NumberBelow100 | NumberBelow1000 | NumberBelow1000000 | NumberBelow1000000000 > ]
	{
		^ <- #1;
	}
)
// N < 100
// zero
// four
// ten
// twenty-six
// ninety-nine
(Number_Rule02_1 :
 	[ NumberBelow10 -> lexNumberValueBelow10]
	{
		^.NumberValue <- #1;
	}
)

(Number_Rule02_2 :
 	[ NumberBelow100 -> NumberBelow10 ]
	{
		^ <- #1;
	}
)

(Number_Rule02_3 :
 	[ NumberBelow100 -> lexNumberValue ]
	{
		^.NumberValue <- #1;
	}
)

// N >= 100 && N < 1000
// a hundred
// one hundred
// one hundred and six
// nine hundreds and ninety-nine
(Number_Rule03:
	[ NumberBelow1000 -> NumberBelow10? lexNumberValueHundred lexConjAnd? NumberBelow100? ] 
	{
		^.NumberValue <- #2;
		if(#1)
		{
	        	^.NumberValue <- ^.NumberValue * #1.NumberValue;
		}
	        if(#4) 
		{
			^.NumberValue <- ^.NumberValue + #4.NumberValue;
        	}
	}
)

(Number_Rule04:
	[ NumberBelow1000 -> NumberBelow100 ] 
	{
		^.NumberValue <- #1.NumberValue;
	}
)

// N >= 1000 && N < 1000000
// thousand
// a thousand
// a thousand and nine 
// two thousand and thirty
// two hundred and six thousand three hundred and seventy-four
// nine hundred and ninety-nine thousand nine hundred and ninety-nine
(Number_Rule05:
	[ NumberBelow1000000 -> NumberBelow1000? lexNumberValueThousand lexConjAnd? NumberBelow1000? ]
	{
		^.NumberValue <- #2;
		if(#1) 
		{
			^.NumberValue <- ^.NumberValue * #1.NumberValue;
		}
		if(#4) 
		{
			^.NumberValue <- ^.NumberValue + #4.NumberValue;
		}
	}
)

(Number_Rule06:
	[ NumberBelow1000000 -> NumberBelow1000 ] 
	{
		^.NumberValue <- #1.NumberValue;
	}
)

// N >= 1000000 && N < 1000000000
// million
// a million
// a million and nine 
// two millions and thirty
// thirty-six millions one hundred and fifty-one
// ninety-nine millions two thousand hundred and fifty-one
// one hundred and ninety-nine millions and three hundred thousand two hundred and fifty-one
// nine hundred and ninety-nine millions nine hundred and ninety-nine thousand nine hundred and ninety-nine
(Number_Rule07:
	[ NumberBelow1000000000 -> NumberBelow1000? lexNumberValueMillion lexConjAnd? NumberBelow1000000? ] 
	{
		^.NumberValue <- #2;
		if(#1) 
		{
			^.NumberValue <- ^.NumberValue * #1.NumberValue;
		}
		if(#4) 
		{
			^.NumberValue <- ^.NumberValue + #4.NumberValue;
		}
	}
)

(Number_Rule08_01:
	[NumberBelow10 -> lexNumberDigits]
	{
		^.NumberValue <- #1;
		if(#1 >= 10)
		{
			Fail();
		}
	}
)

(Number_Rule08_02:
	[NumberBelow100 -> lexNumberDigits]
	{
		^.NumberValue <- #1;
		if(#1 >= 100)
		{
			Fail();
		}
	}
)

(Number_Rule08_03:
	[NumberBelow1000 -> lexNumberDigits]
	{
		^.NumberValue <- #1;
		if(#1 >= 1000)
		{
			Fail();
		}
	}
)


(Number_Rule08_04:
	[NumberBelow1000000 -> lexNumberDigits]
	{
		^.NumberValue <- #1;
		if(#1 >= 1000000)
		{
			Fail();
		}
	}
)

(Number_Rule08_05:
	[NumberBelow1000000000 -> lexNumberDigits]
	{
		^.NumberValue <- #1;
		if(#1 >= 1000000000)
		{
			Fail();
		}
	}
)

// TODO Complete the test corpus
// one thousand eleven
// twenty-one thousand eleven
// seven hundred twenty-one thousand eleven
// one million two hundred fifty-six thousand seven hundred twenty-one
// thirty-one million two hundred fifty-six thousand seven hundred twenty-one
// six hundred thirty-one million two hundred fifty-six thousand seven hundred twenty-one

// TODO Billions? Same as milliard
// one billion four hundred ninety-two million six hundred thirty-eight thousand five hundred twenty-six
// forty-one billion four hundred ninety-two million six hundred thirty-eight thousand five hundred twenty-six
// nine hundred forty-one billion four hundred ninety-two million six hundred thirty-eight thousand five hundred twenty-six

// TODO Trillions? Quadrillion? ...? https://en.wikipedia.org/wiki/Names_of_large_numbers






