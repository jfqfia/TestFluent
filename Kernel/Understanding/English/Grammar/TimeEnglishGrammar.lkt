// **************************************************
//
//	Rules for times in english.
//
// **************************************************
// 
//	PROJECT	:	Fluency
//	MODULE  :	Kernel
//	PACKAGE	:	Understanding.English.Grammar
//	FILE	:	TimeEnglishGrammar.lkt
// 
// **************************************************

(TimeRule01 :
    	[ DialogueAct -> DialogueTime ] 
	{
		^.CoreDialogueAct.Dimension <- 'inform';
		^.SemanticContent.DialogueTime <- #1;
    	}
)

(TimeRule02_1 :
    	[ DialogueTime -> OffsetTime ] 
	{
		^.OffsetTime <- #1;
	}
)

(TimeRule02_2 :
    	[ DialogueTime -> BaseTime ] 
	{
		^.BaseTime <- #1;
	}
)

// in 10 seconds
(TimeRule03_1 :
    	[ OffsetTime -> lexPrepPlace? lexNumberDigits lexSecond ] 
	{
		^.Hour <- 0;
		^.Minute <- 0;
		^.Second <- #2;
    	}
)

// in 5 minutes
(TimeRule03_2 :
    	[ OffsetTime -> lexPrepPlace? lexNumberDigits lexMinute ] 
	{
		^.Hour <- 0;
		^.Minute <- #2;
		^.Second <- 0;
    	}
)

// in 3 hours
(TimeRule03_3 :
    	[ OffsetTime -> lexPrepPlace? lexNumberDigits lexHour ] 
	{
		^.Hour <- #2;
		^.Minute <- 0;
		^.Second <- 0;
    	}
)

// 10 minutes ago
(TimeRule04 :
    	[ OffsetTime -> OffsetTime lexMetaTemporalModifier ] 
	{
		^.PartOfTheDay <- #1.PartOfTheDay;
		^.Hour <- #1.Hour * #2;
		^.Minute <- #1.Minute * #2;
		^.Second <- #1.Second * #2;
    	}
)

// at hh:mm
(TimeRule05 :
    	[ BaseTime -> lexPrepAt? lexNumberDigits colon lexNumberDigits lexAM?] 
	{
		if(#2 < 24 && #4 < 60)
		{
			^.Hour <- #2;
			^.Minute <- #4;
		}
		else	
		{
			Fail();	
		}
    	}
)

// at hh:mm:ss
(TimeRule06 :
    	[ BaseTime -> lexPrepAt? lexNumberDigits colon lexNumberDigits colon lexNumberDigits lexAM?] 
	{
		if(#2 < 24 && #4 < 60 && #6 < 60)
		{
			^.Hour <- #2;
			^.Minute <- #4;
			^.Second <- #6;
		}
		else
		{
			Fail();
		}
    	}
)

// at hh:mm pm
// at hh:mm:ss pm
(TimeRule07 :
    	[ BaseTime -> BaseTime lexPM] 
	{
		^ <- #1;
		if(^.Hour < 12)
		{
			^.Hour <- ^.Hour + 12;
		}
    	}
)

// At 10 o'clock
// 12 o'clock
(TimeRule08 :
    	[ BaseTime -> lexPrepAt? lexNumberDigits lexBaseMinute] 
	{
		if(#2 < 24)
		{
			^.Hour <- #2;
			^.Minute <- #3;
		}
		else
		{		
			Fail();
		}
    	}
)

// Quarter past 9
// A quarter to 9
// half past 9
(TimeRule09 :
    	[ BaseTime -> lexPrepAt? lexDetUndef? lexBaseMinute lexNumberDigits] 
	{
		if(#4 < 24)
		{
			^.Minute <- #3;
			if(#3 == 45)
			{
				^.Hour <- #4 - 1;
			}
			else
			{
				^.Hour <- #4;
			}	
		}
		else
		{
			Fail();
		}
    	}
)

// 5 minutes to 7
// 5 to 7
// Memory error in lekta engine. If you uncomment this you will get a double free error.
/*
(TimeRule10_1 :
    	[ BaseTime -> lexNumberDigits lexMinute? lexPrepTo lexNumberDigits ] 
	{
		if(#1 < 59 && #4 < 24)
		{
			^.Minute <- 60 - #1;
			^.Hour <- #4 - 1;
		}
		else
		{
			Fail();
		}
    	}
)
*/

// 5 minutes past 7
// 6 past 7
(TimeRule10_2 :
    	[ BaseTime -> lexNumberDigits lexMinute? lexPrepPast lexNumberDigits ] 
	{
		if(#1 < 59 && #4 < 24)
		{
			^.Minute <- #1;
			^.Hour <- #4;
		}
		else
		{
			Fail();
		}
    	}
)

// at noon
// noon
// in the evening
// at night
// in the morning
(TimeRule11 :
	[ BaseTime -> lexPrepAt? lexPrepPlace? lexDetThe? lexPartOfTheDay ]
	{	
		^.PartOfTheDay.PartOfTheDayName <- #4; 
	}
)

// late in the evening
// in the middle of the night
// in the middle of the afternoon
(TimeRule12_1 :
	[ BaseTime -> lexTemporalModifierForPartOfTheDay lexPrepAt? lexPrepPlace? lexDetThe? lexPartOfTheDay ]
	{	
		^.PartOfTheDay.PartOfTheDayName <- #5;
		^.PartOfTheDay.PartOfTheDayInterval <- #1;  
	}
)

// 10 hours and 11 minutes
(TimeRule13 :
	[ BaseTime -> lexNumberDigits lexHour lexConjAnd? lexNumberDigits lexMinute ]
	{	
		if(#1 < 24 && #4 < 59)
		{
			^.Minute <- #4;
			^.Hour <- #1;
		}
		else
		{
			Fail();
		}
	}
)

// 10 hours and 11 minutes and 12 seconds
// 10 hours 11 minutes 12 seconds

// Same compilation error as above if YOU COMMENT THIS (very strange error)
(TimeRule14 :
	[ BaseTime -> lexNumberDigits lexHour lexConjAnd? lexNumberDigits lexMinute lexConjAnd? lexNumberDigits lexSecond]
	{	
		if(#1 < 24 && #4 < 59 && #7 < 59)
		{
			^.Second <- #7;
			^.Minute <- #4;
			^.Hour <- #1;
		}
		else
		{
			Fail();
		}
	}
)

